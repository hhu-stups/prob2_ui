import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.gradle.plugins.ide.eclipse.model.AccessRule

plugins {
	id "com.github.johnrengelman.shadow" version "5.2.0"
	id "com.google.osdetector" version "1.6.2"
	// The OpenJFX plugin does not work before Java 11, so we apply it conditionally in an if block further below.
	id "org.openjfx.javafxplugin" version "0.0.8" apply false
}

apply plugin: 'application'
apply plugin: 'eclipse'

project.group = "de.hhu.stups"
project.version = "1.0.1-SNAPSHOT"

if (project != rootProject) {
	throw new GradleException(
		"""\
			The prob2-ui Gradle build cannot be included as a subproject.
			If you are writing a plugin and need to reference the prob2-ui code as a dependency, replace the include statement in your settings.gradle with an includeBuild statement:
			
				includeBuild "${rootProject.relativePath(project.projectDir)}"
			
			And replace the project(":prob2-ui") dependency in your build.gradle with a compileOnly dependency:
			
				dependencies {
					compileOnly group: "${project.group}", name: "${project.name}", version: "${project.version}"
				}
		""".stripIndent()
	)
}

wrapper {
	gradleVersion = "6.3"
	distributionType = Wrapper.DistributionType.ALL
}

sourceCompatibility="1.8"
targetCompatibility="1.8"

repositories {
	jcenter()
	maven {
		name "sonatype snapshots"
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
}

// Our actual application class is de.prob2.ui.ProB2.
// See the Javadoc of de.prob2.ui.Main for an explanation of why this extra class is necessary.
mainClassName = "de.prob2.ui.Main"

final preloaderClassName = "de.prob2.ui.ProB2Preloader"
applicationDefaultJvmArgs += [
	"-Xss2M",
]

def systemProperties = [
	"javafx.preloader": preloaderClassName,
]
applicationDefaultJvmArgs += systemProperties.collect {k, v -> "-D${k}=${v}".toString()}

// Module export/open declarations for Java 9+
def exports = []
def opens = []

// Required by centerdevice-nsmenufx (as of version 2.1.6; see https://github.com/codecentric/NSMenuFX/issues/29)
exports += [
	"javafx.controls/com.sun.javafx.scene.control",
	"javafx.graphics/com.sun.glass.ui",
	"javafx.graphics/com.sun.javafx.menu",
	"javafx.graphics/com.sun.javafx.tk",
]
opens += [
	"javafx.graphics/com.sun.glass.ui.mac",
	"javafx.graphics/com.sun.javafx.tk.quantum",
]

// Required by controlsfx, because we are still using the Java 8-compatible version.
// This has been fixed as of ControlsFX 11.0.0 (see https://github.com/controlsfx/controlsfx/issues/1100), but that release is only compatible with Java 11.
exports += [
	"javafx.graphics/com.sun.javafx.css",
]

if (JavaVersion.current().java9Compatible) {
	applicationDefaultJvmArgs += exports.collect {"--add-exports=${it}=ALL-UNNAMED".toString()}
	applicationDefaultJvmArgs += opens.collect {"--add-opens=${it}=ALL-UNNAMED".toString()}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

configurations {
	javafxAllPlatforms
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

dependencies {
	implementation group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
	implementation group: "com.fatboyindustrial.gson-javatime-serialisers", name: "gson-javatime-serialisers", version: "1.1.1"
	implementation group: "com.google.code.gson", name: "gson", version: "2.8.6"
	implementation group: "com.google.guava", name: "guava", version: "28.2-jre"
	implementation group: "com.googlecode.java-diff-utils", name: "diffutils", version: "1.3.0"
	implementation group: "commons-cli", name: "commons-cli", version: "1.4"
	implementation group: "de.codecentric.centerdevice", name: "centerdevice-nsmenufx", version: "2.1.7"
	implementation group: "de.hhu.stups", name: "de.prob2.kernel", version: "4.0.0-SNAPSHOT"
	implementation group: "net.harawata", name: "appdirs", version: "1.2.0"
	implementation group: "org.controlsfx", name: "controlsfx", version: "8.40.16"
	implementation group: "org.fxmisc.richtext", name: "richtextfx", version: "0.10.5"
	implementation group: "org.fxmisc.wellbehaved", name: "wellbehavedfx", version: "0.3.3"
	implementation group: "org.hildan.fxgson", name: "fx-gson", version: "3.1.2"
	implementation group: "org.pf4j", name: "pf4j", version: "2.6.0"
	implementation group: "se.sawano.java", name: "alphanumeric-comparator", version: "1.4.1"
	implementation group: "com.github.spullara.mustache.java", name: "compiler", version: "0.9.7"
}

final javafxVersion = "11.0.2"
final javafxModules = [
	"javafx.base",
	"javafx.controls",
	"javafx.fxml",
	"javafx.graphics",
	"javafx.swing",
	"javafx.web",
]

if (JavaVersion.current().java11Compatible) {
	apply plugin: "org.openjfx.javafxplugin"
	javafx {
		version = javafxVersion
		modules = javafxModules
	}
}

["win", "mac", "linux"].each {platform ->
	javafxModules.each {module ->
		dependencies.javafxAllPlatforms(group: "org.openjfx", name: module.replace("javafx.", "javafx-"), version: javafxVersion, classifier: platform)
	}
}

eclipse.classpath.file.whenMerged {
	classpath ->
		classpath.entries.findResult { entry ->
			if (entry.kind == 'con' && entry.path.contains('org.eclipse.jdt.launching.JRE_CONTAINER')) {
					entry.accessRules.add(new AccessRule('accessible', 'javafx/**'))
					entry.accessRules.add(new AccessRule('accessible', 'netscape/**'))
			}
		}
}

final generatedResourcesDir = new File(project.buildDir, "generated-src/main/resources")

sourceSets {
	main {
		resources {
			srcDir(generatedResourcesDir)
		}
	}
}

final helpSourceDir = file("src/main/helpsources")
final helpSourceFiles = fileTree(dir: helpSourceDir, include: ["**/*.md"])
final helpOutputDir = new File(generatedResourcesDir, "de/prob2/ui/helpsystem/helppages")
final helpFilterLuaScript = file("pandoc_links_to_html.lua")
task createHelp {
	inputs.file(helpFilterLuaScript)
	inputs.files(helpSourceFiles)
	outputs.dir(helpOutputDir)

	doLast {
		delete(helpOutputDir)
		helpSourceFiles.each {inputFile ->
			final title = inputFile.name.replaceAll(/\.md$/, '')
			final relativePath = helpSourceDir.toPath().relativize(inputFile.toPath())
			final relativePathWithNewExtension = relativePath.parent.resolve(relativePath.fileName.toString().replaceAll(/\.md$/, ".html"))
			final outputFile = helpOutputDir.toPath().resolve(relativePathWithNewExtension).toFile()
			outputFile.parentFile.mkdirs()
			exec {
				executable = "pandoc"
				args = [
					"--from=gfm",
					"--to=html",
					"--metadata=title=${title}",
					"--standalone",
					"--lua-filter=${helpFilterLuaScript}",
					"--output=${outputFile}",
					"${inputFile}",
				]
			}
		}
	}
}
processResources.dependsOn(createHelp)

def readCurrentGitCommit() {
	def proc = ["git", "rev-parse", "HEAD"].execute(null, project.projectDir)
	def exitCode = proc.waitFor()
	if (exitCode != 0) {
		throw new IllegalStateException("git rev-parse command exited with status code ${exitCode}:\n" + proc.err.readLines().join("\n"))
	}
	return proc.in.readLines()[0]
}

processResources {
	filesMatching("de/prob2/ui/build.properties") {
		expand(version: project.version, commit: readCurrentGitCommit())
	}
}

jar {
	manifest {
		attributes([
			"JavaFX-Preloader-Class": preloaderClassName,
			"Add-Exports": exports.join(" "),
			"Add-Opens": opens.join(" "),
		])
	}
}

final probBinariesByOs = [
	"windows": "win64",
	"osx": "leopard64",
	"linux": "linux64",
].collectEntries {k, v -> [(k): "de/prob/cli/binaries/probcli_${v}.zip".toString()]}

shadowJar {
	// Exclude all probcli zips that are not for the current platform.
	// It's safer to do this with an exclude rule that lists all resources that we know we don't want.
	// This way nothing will break if ProB 2 adds new resources that shouldn't be excluded.
	exclude(probBinariesByOs.findAll {k, v -> k != osdetector.os}.values())
}

task multiPlatformShadowJar(type: ShadowJar) {
	manifest {
		inheritFrom(jar.manifest)
		attributes([
				"Main-Class": mainClassName,
				"Multi-Release": "true",
		])
	}

	archiveClassifier = "multi"
	from(sourceSets.main.output)
	configurations = [
			project.configurations.runtimeClasspath,
	]

	// Java 11 and newer normally don't have JavaFX preinstalled,
	// so include OpenJFX 11 for these Java versions so they can run the jar.
	// We don't bundle OpenJFX for older Java versions,
	// because they usually have JavaFX preinstalled,
	// and there are no easily usable standalone versions of OpenJFX < 11 that we could use anyway.
	into("META-INF/versions/11") {
		from(project.configurations.javafxAllPlatforms.collect {zipTree(it)})
	}
}

if (project.hasProperty("probHome")) {
	allprojects {
		tasks.withType(JavaForkOptions) {
			delegate.systemProperties["prob.home"] = project.probHome
		}
	}
}

final appName = 'ProB2-UI'

// Assumes that Java 14 is installed correctly
task jpackage(type: Exec) {
	// Use single-platform shadowJar
	dependsOn 'shadowJar'

	// Show jpackage --verbose output only if running Gradle with --info (or higher)
	logging.captureStandardOutput(LogLevel.INFO)

	final shadowJarFile = shadowJar.archiveFile.get().asFile
	executable = "jpackage"
	args = [
		"--verbose",
		"--dest", project.distsDir,
		"--input", shadowJarFile.parent,
		"--main-jar", shadowJarFile.name, // file name given to --main-jar is relative to the path given to --input
		'--main-class', mainClassName,
		"--name", appName,
		// Some platforms (Windows, macOS) require that versions follow a strict x.y.z format and don't allow string components like SNAPSHOT.
		"--app-version", project.version.replaceAll(/-SNAPSHOT/, ""),
		"--copyright", "Heinrich-Heine-University, Institut für Softwaretechnik und Programmiersprachen 2020",
		"--vendor", "Heinrich-Heine-University, Institut für Softwaretechnik und Programmiersprachen",
		"--license-file", "LICENSE",
	]

	if (osdetector.os == "windows") {
		args += [
			"--icon", "src/main/deploy/package/windows/prob2-ui.ico",
			"--win-dir-chooser",
			"--win-menu",
			"--win-shortcut",
			"--win-menu-group", appName,
			"--win-upgrade-uuid", "9e004b84-41c5-4c95-8aca-32ac44063ef9",
		]
	} else if (osdetector.os == "osx") {
		args += [
			"--icon", "src/main/deploy/package/macosx/prob2-ui.icns"
		]
	} else if (osdetector.os == "linux") {
		args += [
			"--icon", "src/main/deploy/package/linux/prob2-ui.png",
			"--linux-menu-group", appName,
			"--linux-deb-maintainer", "Michael.Leuschel@hhu.de",
		]
	} else {
		throw new GradleException("Unsupported OS: ${osdetector.os} (expected windows, osx, or linux)")
	}
}
