import org.gradle.plugins.ide.eclipse.model.AccessRule

plugins {
	id "com.github.johnrengelman.shadow" version "4.0.3"
	id "edu.sc.seis.macAppBundle" version "2.3.0"
	// The OpenJFX plugin does not work before Java 11, so we apply it conditionally in an if block further below.
	id "org.openjfx.javafxplugin" version "0.0.6" apply false
	id "org.sonarqube" version "2.7"
}

apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

project.group = "de.hhu.stups"
project.version = "1.0.0-SNAPSHOT"

sourceCompatibility="1.8"
targetCompatibility="1.8"

repositories {
	mavenLocal()
	jcenter()
	maven {
		name "sonatype snapshots"
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
	maven {
		name "sonatype release"
		url "https://oss.sonatype.org/content/repositories/releases"
	}
	maven {
		// For the FontAwesomeFX fork that is compatible with Java 8 and 9:
		// https://github.com/xzel23/fontawesomefx
		name "dua3 Bintray"
		url "https://dl.bintray.com/dua3/public"
	}
}

mainClassName = "de.prob2.ui.ProB2"
final preloaderClassName = "de.prob2.ui.ProB2Preloader"
applicationDefaultJvmArgs += [
	"-Xss2M",
]

def systemProperties = [
	"javafx.preloader": preloaderClassName,
]
applicationDefaultJvmArgs += systemProperties.collect {k, v -> "-D${k}=${v}".toString()}

// Module export/open declarations for Java 9+
def exports = []
def opens = []

// Required by centerdevice-nsmenufx (as of version 2.1.6; see https://github.com/codecentric/NSMenuFX/issues/29)
exports += [
	"javafx.controls/com.sun.javafx.scene.control",
	"javafx.graphics/com.sun.glass.ui",
	"javafx.graphics/com.sun.javafx.menu",
	"javafx.graphics/com.sun.javafx.tk",
]
opens += [
	"javafx.graphics/com.sun.glass.ui.mac",
	"javafx.graphics/com.sun.javafx.tk.quantum",
]

// Required by richtextfx (as of version 0.9.x; planned to be fixed in 0.10, see https://github.com/FXMisc/RichTextFX/issues/776)
exports += [
	"javafx.graphics/com.sun.javafx.geom",
	"javafx.graphics/com.sun.javafx.scene.text",
]
opens += [
	"javafx.graphics/com.sun.javafx.text",
	"javafx.graphics/javafx.scene.text",
]

if (JavaVersion.current().java9Compatible) {
	applicationDefaultJvmArgs += exports.collect {"--add-exports=${it}=ALL-UNNAMED".toString()}
	applicationDefaultJvmArgs += opens.collect {"--add-opens=${it}=ALL-UNNAMED".toString()}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

dependencies {
	compile group: "com.google.code.gson", name: "gson", version: "2.8.5"
	compile group: "com.googlecode.java-diff-utils", name: "diffutils", version: "1.3.0"
	compile group: "de.codecentric.centerdevice", name: "centerdevice-nsmenufx", version: "2.1.6"
	compile group: "de.hhu.stups", name: "de.prob2.kernel", version: "3.2.12-SNAPSHOT", changing: true
	compile group: "de.jensd", name: "fontawesomefx-commons", version: "9.1.2-jpms"
	// Cannot update to the next higher version (4.7.0-9), because its class files are compiled for Java 9.
	compile group: "de.jensd", name: "fontawesomefx-fontawesome", version: "4.7.0-5"
	compile group: "org.fxmisc.richtext", name: "richtextfx", version: "0.9.2"
	compile group: "org.fxmisc.wellbehaved", name: "wellbehavedfx", version: "0.3.3"
	compile group: "org.hildan.fxgson", name: "fx-gson", version: "3.1.2"
	compile group: "org.pf4j", name: "pf4j", version: "2.6.0"
	compile group: "se.sawano.java", name: "alphanumeric-comparator", version: "1.4.1"
}

if (JavaVersion.current().java11Compatible) {
	apply plugin: "org.openjfx.javafxplugin"
	javafx {
		modules = [
			"javafx.base",
			"javafx.controls",
			"javafx.fxml",
			"javafx.graphics",
			"javafx.swing",
			"javafx.web",
		]
	}
}

eclipse.classpath.file.whenMerged {
	classpath ->
		classpath.entries.findResult { entry ->
			if (entry.kind == 'con' && entry.path.contains('org.eclipse.jdt.launching.JRE_CONTAINER')) {
					entry.accessRules.add(new AccessRule('accessible', 'javafx/**'))
					entry.accessRules.add(new AccessRule('accessible', 'netscape/**'))
			}
		}
}

def readCurrentGitCommit() {
	def proc = ["git", "rev-parse", "HEAD"].execute(null, project.projectDir)
	def exitCode = proc.waitFor()
	if (exitCode != 0) {
		throw new IllegalStateException("git rev-parse command exited with status code ${exitCode}:\n" + proc.err.readLines().join("\n"))
	}
	return proc.in.readLines()[0]
}

processResources {
	doFirst {
		def f = file("src/main/resources/de/prob2/ui/build.properties")
		f.delete()
		f.append("version=${project.version}\n")
		f.append("commit=${readCurrentGitCommit()}\n")
	}
}

jar {
	manifest {
		attributes([
			"JavaFX-Preloader-Class": preloaderClassName,
			"JavaFX-Application-Class": mainClassName,
			"Add-Exports": exports.join(" "),
			"Add-Opens": opens.join(" "),
		])
	}
}

macAppBundle {
	dmgName = project.name + "-" + project.version + "-mac"
	icon = "src/main/deploy/package/macosx/prob2-ui.icns"
	bundleJRE = false
	jreHome = null
	javaProperties = systemProperties
}

macAppBundle.mainClassName = mainClassName

sonarqube {
	properties {
		property("sonar.java.binaries", sourceSets.main.java.outputDir.path)
		
		property("sonar.projectKey", "prob2-ui")
		property("sonar.projectName", "prob2-ui")
		
		property("sonar.exclusions", [
			"gradle/wrapper/*",
			"build/**",
			// Sonar currently throws exceptions when parsing ProB2Module.java, so it's excluded for now.
			"src/main/java/de/prob2/ui/internal/ProB2Module.java",
			"src/main/resources/codemirror/clike.js",
			"src/main/resources/codemirror/closebrackets.js",
			"src/main/resources/codemirror/codemirror.css",
			"src/main/resources/codemirror/codemirror.js",
			"src/main/resources/codemirror/jquery.js",
			"src/main/resources/codemirror/matchbrackets.js",
		].join(","))
		
		property("sonar.login", System.getenv("SONAR_TOKEN"))
	}
}

if( rootProject == project) {
	wrapper {
		gradleVersion = "5.2.1"
		distributionType = Wrapper.DistributionType.ALL
	}
}

final helpDir = file("src/main/resources/help")
task createHelp(type: Exec) {
	inputs.dir(helpDir)
	outputs.dir(helpDir)
	commandLine './help.sh'
}

task cleanHelp(type: Delete) {
	delete(fileTree(dir: helpDir, include: ["**/*.adoc", "**/*.html"]))
}

if (!project.hasProperty("skipHelp") || project.skipHelp != "true") {
	processResources.dependsOn(createHelp)
	clean.dependsOn(cleanHelp)
}

tasks.withType(JavaCompile) {
	options.compilerArgs += ["-proc:none"]
}

if (project.hasProperty("probHome")) {
	allprojects {
		tasks.withType(JavaForkOptions) {
			delegate.systemProperties["prob.home"] = project.probHome
		}
	}
}
