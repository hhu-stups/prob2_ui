import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

import org.gradle.plugins.ide.eclipse.model.AccessRule

plugins {
	id "com.github.johnrengelman.shadow" version "5.2.0"
	id "edu.sc.seis.macAppBundle" version "2.3.0"
	// The OpenJFX plugin does not work before Java 11, so we apply it conditionally in an if block further below.
	id "org.openjfx.javafxplugin" version "0.0.8" apply false
}

apply plugin: 'application'
apply plugin: 'eclipse'

project.group = "de.hhu.stups"
project.version = "1.0.1-SNAPSHOT"

if (project != rootProject) {
	throw new GradleException(
		"""\
			The prob2-ui Gradle build cannot be included as a subproject.
			If you are writing a plugin and need to reference the prob2-ui code as a dependency, replace the include statement in your settings.gradle with an includeBuild statement:
			
				includeBuild "${rootProject.relativePath(project.projectDir)}"
			
			And replace the project(":prob2-ui") dependency in your build.gradle with a compileOnly dependency:
			
				dependencies {
					compileOnly group: "${project.group}", name: "${project.name}", version: "${project.version}"
				}
		""".stripIndent()
	)
}

sourceCompatibility="1.8"
targetCompatibility="1.8"

repositories {
	jcenter()
	maven {
		name "sonatype snapshots"
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
}

// Our actual application class is de.prob2.ui.ProB2.
// See the Javadoc of de.prob2.ui.Main for an explanation of why this extra class is necessary.
mainClassName = "de.prob2.ui.Main"

def appName = 'ProB2-UI'
def appVersion = '1.0.1'

task copyDependencies(type: Copy) {
	dependsOn 'build'

	from configurations.runtime
	into "build/libs"
}


task jpackage(type: Exec) {
	dependsOn 'clean'
	dependsOn 'copyDependencies'

	def iconPath;

	if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
		iconPath = 'src/main/deploy/package/macosx/prob2-ui.icns'
	} else if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
		iconPath = 'src/main/deploy/package/windows/prob2-ui.ico'
	} else {
		iconPath = 'src/main/deploy/package/linux/prob2-ui.png'
	}

	commandLine "jpackage", '--icon', "${iconPath}",
			"--name", "${appName}", '--input', 'build/libs', '--main-class', "${mainClassName}",
			'--main-jar', "prob2-ui-${project.version}-all.jar",
			'--app-version', "${appVersion}", '--copyright', "\"Heinrich-Heine-University, Institut fÃ¼r Softwaretechnik und Programmiersprachen 2020\""
}

final preloaderClassName = "de.prob2.ui.ProB2Preloader"
applicationDefaultJvmArgs += [
	"-Xss2M",
]

def systemProperties = [
	"javafx.preloader": preloaderClassName,
]
applicationDefaultJvmArgs += systemProperties.collect {k, v -> "-D${k}=${v}".toString()}

// Module export/open declarations for Java 9+
def exports = []
def opens = []

// Required by centerdevice-nsmenufx (as of version 2.1.6; see https://github.com/codecentric/NSMenuFX/issues/29)
exports += [
	"javafx.controls/com.sun.javafx.scene.control",
	"javafx.graphics/com.sun.glass.ui",
	"javafx.graphics/com.sun.javafx.menu",
	"javafx.graphics/com.sun.javafx.tk",
]
opens += [
	"javafx.graphics/com.sun.glass.ui.mac",
	"javafx.graphics/com.sun.javafx.tk.quantum",
]

// Required by controlsfx, because we are still using the Java 8-compatible version.
// This has been fixed as of ControlsFX 11.0.0 (see https://github.com/controlsfx/controlsfx/issues/1100), but that release is only compatible with Java 11.
exports += [
	"javafx.graphics/com.sun.javafx.css",
]

if (JavaVersion.current().java9Compatible) {
	applicationDefaultJvmArgs += exports.collect {"--add-exports=${it}=ALL-UNNAMED".toString()}
	applicationDefaultJvmArgs += opens.collect {"--add-opens=${it}=ALL-UNNAMED".toString()}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

configurations {
	javafxAllPlatforms
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

dependencies {
	implementation group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
	implementation group: "com.fatboyindustrial.gson-javatime-serialisers", name: "gson-javatime-serialisers", version: "1.1.1"
	implementation group: "com.google.code.gson", name: "gson", version: "2.8.6"
	implementation group: "com.google.guava", name: "guava", version: "28.2-jre"
	implementation group: "com.googlecode.java-diff-utils", name: "diffutils", version: "1.3.0"
	implementation group: "commons-cli", name: "commons-cli", version: "1.4"
	implementation group: "de.codecentric.centerdevice", name: "centerdevice-nsmenufx", version: "2.1.7"
	implementation group: "de.hhu.stups", name: "de.prob2.kernel", version: "4.0.0-SNAPSHOT"
	implementation group: "net.harawata", name: "appdirs", version: "1.2.0"
	implementation group: "org.controlsfx", name: "controlsfx", version: "8.40.16"
	implementation group: "org.fxmisc.richtext", name: "richtextfx", version: "0.10.3"
	implementation group: "org.fxmisc.wellbehaved", name: "wellbehavedfx", version: "0.3.3"
	implementation group: "org.hildan.fxgson", name: "fx-gson", version: "3.1.2"
	implementation group: "org.pf4j", name: "pf4j", version: "2.6.0"
	implementation group: "se.sawano.java", name: "alphanumeric-comparator", version: "1.4.1"
}

final javafxVersion = "11.0.2"
final javafxModules = [
	"javafx.base",
	"javafx.controls",
	"javafx.fxml",
	"javafx.graphics",
	"javafx.swing",
	"javafx.web",
]

if (JavaVersion.current().java11Compatible) {
	apply plugin: "org.openjfx.javafxplugin"
	javafx {
		version = javafxVersion
		modules = javafxModules
	}
}

["win", "mac", "linux"].each {platform ->
	javafxModules.each {module ->
		dependencies.javafxAllPlatforms(group: "org.openjfx", name: module.replace("javafx.", "javafx-"), version: javafxVersion, classifier: platform)
	}
}

eclipse.classpath.file.whenMerged {
	classpath ->
		classpath.entries.findResult { entry ->
			if (entry.kind == 'con' && entry.path.contains('org.eclipse.jdt.launching.JRE_CONTAINER')) {
					entry.accessRules.add(new AccessRule('accessible', 'javafx/**'))
					entry.accessRules.add(new AccessRule('accessible', 'netscape/**'))
			}
		}
}

final generatedResourcesDir = new File(project.buildDir, "generated-src/main/resources")

sourceSets {
	main {
		resources {
			srcDir(generatedResourcesDir)
		}
	}
}

def readCurrentGitCommit() {
	def proc = ["git", "rev-parse", "HEAD"].execute(null, project.projectDir)
	def exitCode = proc.waitFor()
	if (exitCode != 0) {
		throw new IllegalStateException("git rev-parse command exited with status code ${exitCode}:\n" + proc.err.readLines().join("\n"))
	}
	return proc.in.readLines()[0]
}

processResources {
	filesMatching("de/prob2/ui/build.properties") {
		expand(version: project.version, commit: readCurrentGitCommit())
	}
}

jar {
	manifest {
		attributes([
			"JavaFX-Preloader-Class": preloaderClassName,
			"Add-Exports": exports.join(" "),
			"Add-Opens": opens.join(" "),
		])
	}
}

task multiPlatformShadowJar(type: ShadowJar) {
	manifest {
		inheritFrom(jar.manifest)
		attributes(["Main-Class": mainClassName])
	}
	
	archiveClassifier = "multi"
	from(sourceSets.main.output)
	configurations = [
		project.configurations.runtimeClasspath,
		project.configurations.javafxAllPlatforms,
	]
}

macAppBundle {
	dmgName = project.name + "-" + project.version + "-mac"
	icon = "src/main/deploy/package/macosx/prob2-ui.icns"
	bundleJRE = false
	jreHome = null
	javaProperties = systemProperties
}

macAppBundle.mainClassName = mainClassName

wrapper {
	gradleVersion = "6.3"
	distributionType = Wrapper.DistributionType.ALL
}

final helpSourceDir = file("src/main/helpsources")
final helpSourceFiles = fileTree(dir: helpSourceDir, include: ["**/*.md"])
final helpOutputDir = new File(generatedResourcesDir, "de/prob2/ui/helpsystem/helppages")
final helpFilterLuaScript = file("pandoc_links_to_html.lua")
task createHelp {
	inputs.file(helpFilterLuaScript)
	inputs.files(helpSourceFiles)
	outputs.dir(helpOutputDir)

	doLast {
		delete(helpOutputDir)
		helpSourceFiles.each {inputFile ->
			final title = inputFile.name.replaceAll(/\.md$/, '')
			final relativePath = helpSourceDir.toPath().relativize(inputFile.toPath())
			final relativePathWithNewExtension = relativePath.parent.resolve(relativePath.fileName.toString().replaceAll(/\.md$/, ".html"))
			final outputFile = helpOutputDir.toPath().resolve(relativePathWithNewExtension).toFile()
			outputFile.parentFile.mkdirs()
			exec {
				executable = "pandoc"
				args = [
					"--from=gfm",
					"--to=html",
					"--metadata=title=${title}",
					"--standalone",
					"--lua-filter=${helpFilterLuaScript}",
					"--output=${outputFile}",
					"${inputFile}",
				]
			}
		}
	}
}
processResources.dependsOn(createHelp)

if (project.hasProperty("probHome")) {
	allprojects {
		tasks.withType(JavaForkOptions) {
			delegate.systemProperties["prob.home"] = project.probHome
		}
	}
}
